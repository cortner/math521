{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg; Pkg.activate(\"..\")\n",
    "using SparseArrays, LinearAlgebra, Ferrite, LaTeXStrings\n",
    "using FerriteViz, WGLMakie, Makie\n",
    "Makie.inline!(true);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In this notebook we will use the code `Ferrite.jl` to solve our simplest model problem, \n",
    "$$\\begin{aligned} \n",
    " - \\Delta u  &= f, \\qquad \\Omega, \\\\ \n",
    "          u &= 0, \\qquad \\partial\\Omega\n",
    "\\end{aligned}$$ \n",
    "In the assignment we will generalize this to some more intersting problems."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "grid = generate_grid(Triangle, (20, 20))\n",
    "FerriteViz.wireframe(grid, markersize = 10, strokewidth = 2, \n",
    "                     figure = (resolution=(500,500),))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# here we specify the finite element space and the quadrature rule. \n",
    "# ip_geo specifies the space for isoparametric elements i.e. \n",
    "# elements that can have curved boundaries. Ferrite forces us \n",
    "# to specify this - but we can just ignore it from here on.\n",
    "dim = 2\n",
    "ip = Lagrange{dim, RefTetrahedron, 2}()\n",
    "ip_geo = Lagrange{dim, RefTetrahedron, 1}()\n",
    "qr = QuadratureRule{dim, RefTetrahedron}(4)\n",
    "# qr.points and wr.weights ... \n",
    "\n",
    "# the next object facilitates the process of evaluating \n",
    "# shape functions and their derivatives at quadrature \n",
    "# points; it pre-evaluates them in the reference \n",
    "# element and then allows us to access those values \n",
    "# during the assembly\n",
    "cellvalues = CellScalarValues(qr, ip, ip_geo)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# a DOF handler is an object that manages how degrees \n",
    "# of freedom of FE functions are stored, e.g. for P1\n",
    "# this object will assigne DOFs to the element nodes\n",
    "# for P2 it assigns DOFs to nodes and edges. \n",
    "dh = DofHandler(grid)\n",
    "# here we register a scalar-valued function \"u\" \n",
    "# with the DOF handler. \n",
    "add!(dh, :u, 1, ip)\n",
    "close!(dh)\n",
    "\n",
    "# Once you know the mesh and the DOFs you can already \n",
    "# determine the sparsity pattern of the system matrix\n",
    "# this is now precomputed for later use. \n",
    "K = create_sparsity_pattern(dh)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# The Dirichlet Boundary condition is handled \n",
    "# as a constraint. Typical FEM software frameworks\n",
    "# usually have objects the manage constraints for us\n",
    "ch = ConstraintHandler(dh)\n",
    "\n",
    "# The next line is just a simple trick to get the \n",
    "# boundary information - this can be more complicated \n",
    "# in general. \n",
    "∂Ω = union(getfaceset.(Ref(grid), [\"left\", \"right\", \"top\", \"bottom\"])...)\n",
    "# now we are generate a Dirichlet bc constraint ...\n",
    "dbc = Dirichlet(:u, ∂Ω, (x, t) -> 0)\n",
    "# ... and register it with the constraint handler \n",
    "add!(ch, dbc)\n",
    "close!(ch)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# We are now ready to assemble the finite element system. \n",
    "# This is normally done in two functions, \n",
    "#   (1) assemble_global!  : the outer assembly loop, \n",
    "#                           this is just boilerplate \n",
    "#   (2) assemble_element! : the assembly work on an individual element, \n",
    "#                           here the problem-specific work happens\n",
    "#\n",
    "# the ! suffix is a Julia convention to indicate that the functions \n",
    "# modify the input argument(s)\n",
    "\n",
    "function assemble_global!(cellvalues, dh, K, ffun)\n",
    "   # we pre-allocate the element stiffness matrix and element force vector\n",
    "   # these will be passed to the element assembly to avoid many allocations\n",
    "   n_basefuncs = getnbasefunctions(cellvalues)\n",
    "   Ke = zeros(n_basefuncs, n_basefuncs)\n",
    "   fe = zeros(n_basefuncs)\n",
    "    \n",
    "   # Allocate global force vector f\n",
    "   f = zeros(ndofs(dh))\n",
    "    \n",
    "   # Create an assembler: this object knows how to write \n",
    "   # the local arrays Ke, fe into the global arrays K, f \n",
    "   assembler = start_assemble(K, f)\n",
    "    \n",
    "   # Loop over all cells; this is managed by the DOF handler \n",
    "   # since the `cell` comes with information about local \n",
    "   # DOFs attached.\n",
    "   for cell in CellIterator(dh)\n",
    "       # Reinitialize cellvalues for this cell\n",
    "       # `cellvalues` has iterators attached that need \n",
    "       # to be reset. This seems unnecessary and probably \n",
    "       # just a poor code design decision. \n",
    "       reinit!(cellvalues, cell)\n",
    "       # ==========================================\n",
    "       # Compute element contribution; \n",
    "       # this is where the actual work happens\n",
    "       assemble_element!(Ke, fe, cell, cellvalues, ffun)\n",
    "       # ==========================================\n",
    "       # local-to-global assemble Ke and fe into K and f\n",
    "       assemble!(assembler, celldofs(cell), Ke, fe)\n",
    "   end\n",
    "   return K, f\n",
    "end\n",
    "\n",
    "function assemble_element!(Ke, fe, cell, cellvalues, ffun)\n",
    "   # number of local basis functions    \n",
    "   n_basefuncs = getnbasefunctions(cellvalues)\n",
    "   # Reset the local arrays \n",
    "   fill!(Ke, 0); fill!(fe, 0)\n",
    "   # precompute the cell coordinates \n",
    "   cell_coords = getcoordinates(cell)\n",
    "    \n",
    "   # Loop over quadrature points\n",
    "   for i_q in 1:getnquadpoints(cellvalues)\n",
    "       # Get the quadrature weight for the current quad point\n",
    "       # this includes the det(F) terms. It can be thought of \n",
    "       # as the volume element (hence dΩ)\n",
    "       dΩ = getdetJdV(cellvalues, i_q)\n",
    "        \n",
    "       # evaluate f at the quadrature point \n",
    "       ξ_q = spatial_coordinate(cellvalues, i_q, cell_coords)\n",
    "       f_q = ffun(ξ_q)\n",
    "        \n",
    "       # Loop over test shape functions (basis functions)\n",
    "       for i in 1:n_basefuncs\n",
    "           # get the values v = ψ_i(ξ_q), ∇v = ∇ψ_i(ξ_q)\n",
    "           v  = shape_value(cellvalues, i_q, i)\n",
    "           ∇v = shape_gradient(cellvalues, i_q, i)\n",
    "            \n",
    "           # Add contribution to fe\n",
    "           fe[i] += f_q * v * dΩ\n",
    "            \n",
    "           # Loop over trial shape functions\n",
    "           for j in 1:n_basefuncs\n",
    "               ∇u = shape_gradient(cellvalues, i_q, j)\n",
    "               # Add contribution to Ke\n",
    "               Ke[i, j] += dot(∇v, ∇u) * dΩ\n",
    "           end\n",
    "       end\n",
    "   end\n",
    "   return Ke, fe\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# we can assemble the system\n",
    "ffun = ξ -> 5.0\n",
    "K, f = assemble_global!(cellvalues, dh, K, ffun)\n",
    "\n",
    "# apply the constraints\n",
    "apply!(K, f, ch)\n",
    "\n",
    "# solve the resulting linear system\n",
    "u = K \\ f;\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# visualize the solution (always in a separate cell!)\n",
    "plotter = FerriteViz.MakiePlotter(dh, u)\n",
    "\n",
    "# FerriteViz.solutionplot(plotter,field=:u, figure = (resolution = (500,500),))\n",
    "\n",
    "fig = FerriteViz.surface(plotter, field=:u, \n",
    "                   figure = (resolution = (700,700,),))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# In a way this code looks messier and more difficult \n",
    "# to read than our naive P1 code. But the point is that \n",
    "# it is much easier to change the finite element method\n",
    "# for example if we want to change to a different order \n",
    "# this is now straightforward: \n",
    "\n",
    "function setup_fem(N, p)\n",
    "    grid = generate_grid(Triangle, (N, N))\n",
    "    dim = 2\n",
    "    ip = Lagrange{dim, RefTetrahedron, p}()\n",
    "    ip_geo = Lagrange{dim, RefTetrahedron, 1}()\n",
    "    qr = QuadratureRule{dim, RefTetrahedron}(3*p)\n",
    "    cellvalues = CellScalarValues(qr, ip, ip_geo)\n",
    "    dh = DofHandler(grid)\n",
    "    add!(dh, :u, 1, ip)\n",
    "    close!(dh)\n",
    "    K = create_sparsity_pattern(dh)\n",
    "    ch = ConstraintHandler(dh)\n",
    "    ∂Ω = union(getfaceset.(Ref(grid), [\"left\", \"right\", \"top\", \"bottom\"])...)\n",
    "    dbc = Dirichlet(:u, ∂Ω, (x, t) -> 0)\n",
    "    add!(ch, dbc)\n",
    "    close!(ch)    \n",
    "    return cellvalues, dh, ch, K\n",
    "end\n",
    "\n",
    "function solve_fem(cellvalues, dh, ch, K, ffun)\n",
    "    K, f = assemble_global!(cellvalues, dh, K, ξ -> ffun(ξ));\n",
    "    apply!(K, f, ch)\n",
    "    u = K \\ f;\n",
    "    return u \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# e.g. with a smaller grid size but cubic elements\n",
    "cellvalues, dh, ch, K = setup_fem(8, 3)\n",
    "\n",
    "# solve \n",
    "u = solve_fem(cellvalues, dh, ch, K, ξ -> 5.0)\n",
    "\n",
    "# visualize : we can't see a difference in the solution\n",
    "# but this is good of course. We will look at errors next.\n",
    "fig = FerriteViz.surface(plotter, field=:u, \n",
    "                   figure = (resolution = (700,700,),))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Method of Manufactured Solutions\n",
    "\n",
    "We specify a solution $u(x)$ and compute $f = - \\Delta u(x)$ via automatic differentiation. Then we use $f$ as the input into our FEM.\n",
    "\n",
    "CAREFUL: Sometimes when we do this we accidentally give $u$ more regularity than is natural!! We will return to this soon."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using ForwardDiff, LinearAlgebra\n",
    "\n",
    "u_ex(x) = sin(π*x[1]) * sin(π*x[2])\n",
    "ffun = x -> - tr( ForwardDiff.hessian(u_ex, x) )\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "With the exact solution in hand, we can now compute the errors. And we do it via quadrature! "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "- N : # grid pts in each coordinate direction, h = 1/N\n",
    "- k : polynomial order of the FEM  (Lagrange element)\n",
    "- ffun : forcing function\n",
    "\"\"\"\n",
    "function fem_errors(N, k, ffun)\n",
    "    cellvalues, dh, ch, K = setup_fem(N, k)\n",
    "    u = solve_fem(cellvalues, dh, ch, K, ffun)\n",
    "    return compute_errors(cellvalues, dh, u, u_ex)\n",
    "end\n",
    "\n",
    "function compute_errors(cellvalues, dh, u, u_ex)\n",
    "    n_basefuncs = getnbasefunctions(cellvalues)\n",
    "    err_L2 = 0.0 \n",
    "    err_H1 = 0.0 \n",
    "    \n",
    "    # loop over cells (= elements)\n",
    "    for cell in CellIterator(dh)\n",
    "        reinit!(cellvalues, cell)        \n",
    "        n_basefuncs = getnbasefunctions(cellvalues)\n",
    "        cell_coords = getcoordinates(cell)\n",
    "        \n",
    "        # we also need the local degrees of freedom \n",
    "        u_cell = u[cell.dofs]\n",
    "    \n",
    "        vK = 0.0\n",
    "        for i_q in 1:getnquadpoints(cellvalues)\n",
    "            dΩ = getdetJdV(cellvalues, i_q)\n",
    "            ξ_q = spatial_coordinate(cellvalues, i_q, cell_coords)\n",
    "            u_q   = u_ex(ξ_q)\n",
    "            ∇u_q  = ForwardDiff.gradient(u_ex, ξ_q)\n",
    "            uh_q  = function_value(cellvalues, i_q, u_cell)\n",
    "            ∇uh_q = function_gradient(cellvalues, i_q, u_cell)\n",
    "            err_L2 += dΩ * (u_q - uh_q)^2\n",
    "            err_H1 += dΩ * norm(∇u_q - ∇uh_q)^2\n",
    "        end\n",
    "    end\n",
    "    return sqrt(err_L2), sqrt(err_H1)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "NN = [4, 8, 16, 32, 64, 128]\n",
    "errs_L2 = Float64[] \n",
    "errs_H1 = Float64[]\n",
    "for N in NN\n",
    "    err_L2, err_H1 = fem_errors(N, 1, ffun)\n",
    "    push!(errs_L2, err_L2)\n",
    "    push!(errs_H1, err_H1)\n",
    "end\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# ... and visualize them\n",
    "fig = Figure(size = (400, 400); fontsize=30)\n",
    "ax = Axis(fig[1, 1], xlabel = L\"h^{-1}\", ylabel = L\"\\text{error}\", \n",
    "          xscale = log10, yscale = log10,\n",
    "          title = \"Error P1-FEM\")\n",
    "scatterlines!(NN, errs_L2; linewidth=5, markersize=20, label=L\"L^2\")\n",
    "scatterlines!(NN, errs_H1; linewidth=5, markersize=20, label = L\"H^1\")\n",
    "NN1 = NN[3:5]\n",
    "lines!(NN1, 6 ./ NN1; color=:black, linewidth=3, label = L\"h, h^2\")\n",
    "lines!(NN1, 5 ./ NN1.^2; color=:black, linewidth=3)\n",
    "axislegend(ax)\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Equipped with the entire `Ferrite.jl` infrastructure behind us we can now do the same with Pk FEM with no changes to the script."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "k = 3   # try different k\n",
    "NN = [4, 8, 16, 32, 64]\n",
    "errs_L2 = Float64[] \n",
    "errs_H1 = Float64[]\n",
    "for N in NN\n",
    "    err_L2, err_H1 = fem_errors(N, k, ffun)\n",
    "    push!(errs_L2, err_L2)\n",
    "    push!(errs_H1, err_H1)\n",
    "end\n",
    "\n",
    "# as an exception we break our rule of doing computation \n",
    "# and visualization in separate cells. \n",
    "\n",
    "fig = Figure(size = (400, 400); fontsize=30)\n",
    "ax = Axis(fig[1, 1], xlabel = L\"h^{-1}\", ylabel = L\"\\text{error}\", \n",
    "          xscale = log10, yscale = log10,\n",
    "          title = \"Error P$k-FEM\")\n",
    "scatterlines!(NN, errs_L2; linewidth=5, markersize=20, label=L\"L^2\")\n",
    "scatterlines!(NN, errs_H1; linewidth=5, markersize=20, label = L\"H^1\")\n",
    "NN1 = NN[3:5]\n",
    "lines!(NN1, 6 ./ NN1.^k; color=:black, linewidth=3, \n",
    "       label = latexstring(\"h^$k, h^$(k+1)\"))\n",
    "lines!(NN1, 5 ./ NN1.^(k+1); color=:black, linewidth=3)\n",
    "axislegend(ax)\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Or it might be interested to plot there errors for different methods on a single plot to compare. \n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "KK = 1:5\n",
    "NN = [ [4, 8, 16, 32, 64, 128], \n",
    "       [4, 8, 16, 32, 64, 128], \n",
    "       [4, 8, 16, 32, 64, 128], \n",
    "       [4, 8, 16, 32, 64, 128], \n",
    "       [4, 8, 16, 32, 64], ]\n",
    "        \n",
    "errs_L2 = [ Float64[] for _ = 1:length(KK) ] \n",
    "errs_H1 = [ Float64[] for _ = 1:length(KK) ]\n",
    "    \n",
    "for k in KK, N in NN[k] \n",
    "    err_L2, err_H1 = fem_errors(N, k, ffun)\n",
    "    push!(errs_L2[k], err_L2)\n",
    "    push!(errs_H1[k], err_H1)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig = Figure(size = (400, 400); fontsize=30)\n",
    "ax = Axis(fig[1, 1], xlabel = L\"h^{-1}\", ylabel = L\"|| \\nabla u - \\nabla u_h ||_{L^2}\", \n",
    "          xscale = log10, yscale = log10,\n",
    "          title = L\"H^1 \\text{Error Pk-FEM}\")\n",
    "for k in KK \n",
    "    scatterlines!(NN[k], errs_H1[k]; linewidth=5, markersize=20, \n",
    "                  label = latexstring(\"k = $k\"))\n",
    "    NN1 = (k < 5) ? NN[k][3:5] : NN[k][3:4]\n",
    "    lines!(NN1, 6/k ./ NN1.^k; color=:black, linewidth=3)\n",
    "end\n",
    "axislegend(ax, position = :lb)\n",
    "fig"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
