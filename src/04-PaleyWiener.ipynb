{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "9faef41d",
   "metadata": {},
   "source": [
    "# Exploration of the Paley-Wiener Thm\n",
    "\n",
    "In this notebook, we briefly explore numerically the result of a variant of the Paley-Wiener theorem: if $f$ is analytic and $2\\pi$-periodic, then there exists $\\alpha > 0$ such that $|\\hat{f}_k| \\leq C e^{-\\alpha |k|}$. The $\\alpha$ can be chosen such that $f$ is analytic in the strip $\\Omega_\\alpha := \\{ z + i y : |y| \\leq \\alpha \\}$. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "51394108",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg; Pkg.activate(\".\")\n",
    "using QuadGK, LinearAlgebra, LaTeXStrings, CairoMakie"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ead77e4b",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "This is a very naive method to compute the \n",
    "Fourier coefficients taken from our \"naive\"\n",
    "spectral method. We will soon learn about \n",
    "much more efficient schemes based on the FFT.\n",
    "\"\"\"\n",
    "function compute_fcoeffs(f_fun, N)\n",
    "    fhat = zeros(ComplexF64, 2*N+1)\n",
    "    kgrid = -N:N\n",
    "    for (i, k) in enumerate(kgrid)\n",
    "        g = x -> f_fun(x) * exp(-im * k * x)\n",
    "        fhat[i] = quadgk(g, -pi, pi; rtol = 1e-6, atol=1e-6)[1] / (2*pi)\n",
    "    end\n",
    "    return fhat, kgrid\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "83a4a6e0",
   "metadata": {},
   "source": [
    "Our model problem is a periodic version of the Witch of Agnesi, \n",
    "$$\n",
    "  f(x) = \\frac{1}{1+ c^2 \\sin^2(x/2)}\n",
    "$$\n",
    "It's analytic extension into the complex plane has a singularity \n",
    "at $z = \\pm i \\alpha$, where $\\alpha = \\sinh^{-1}(2/c)$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a32db46a",
   "metadata": {},
   "outputs": [],
   "source": [
    "f, α = let c = 3\n",
    "    (x -> 1 / (1 + c^2 * sin(x/2)^2)), asinh(2/c)\n",
    "end\n",
    "\n",
    "xp = range(-π, π, length=200)\n",
    "fig0 = Figure(size = (400, 250)); ax0 = Axis(fig0[1,1])\n",
    "lines!(xp, f.(xp)); fig0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "28d00a75",
   "metadata": {},
   "outputs": [],
   "source": [
    "F̂, kk = compute_fcoeffs(f, 20);\n",
    "\n",
    "fig1 = Figure(size = (400, 250))\n",
    "ax1 = Axis(fig1[1,1], yscale = log10, xlabel = L\"|k|\", ylabel = L\"|\\hat{f}_k|\")\n",
    "scatter!(abs.(kk), abs.(F̂), label = L\"\\hat{f}_k\")\n",
    "lines!( [10, 18], 0.1*exp.(-α * [10, 18]), linestyle = :dash, color = :black, label = L\"\\exp(-\\alpha N)\")\n",
    "axislegend(ax1, position = :rt)\n",
    "fig1 "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "88b94a91",
   "metadata": {},
   "source": [
    "We can quickly confirm the resulting rate of approximation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aac45c0d",
   "metadata": {},
   "outputs": [],
   "source": [
    "function eval_trig(x, uhat) \n",
    "    N = (length(uhat)-1) ÷ 2\n",
    "    return real( sum(uhat[k+N+1] * exp(im * x * k) for k = -N:N) )\n",
    "end\n",
    "\n",
    "NN = [4, 8, 12, 16, 20, 24, 28]\n",
    "xe = range(-π, π, length=1000)\n",
    "f_ex = f.(xe)\n",
    "err_max = zeros(length(NN))\n",
    "for (i, N) in enumerate(NN)\n",
    "    Ĝ, _ = compute_fcoeffs(f, N);\n",
    "    fN = eval_trig.(xe, Ref(Ĝ))\n",
    "    err_max[i] = norm(f_ex - fN, Inf)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "918c2d32",
   "metadata": {},
   "outputs": [],
   "source": [
    "fig2 = Figure(size = (350, 250))\n",
    "ax2 = Axis(fig2[1,1], yscale = log10, xlabel = \"N\", ylabel = L\"|| f - f_N ||_{L^\\infty}\")\n",
    "scatterlines!(NN, err_max, label = \"error\")\n",
    "lines!(NN[4:end], exp.(-α * NN[4:end]), linestyle = :dash, color = :black, label = L\"\\exp(-\\alpha N)\")\n",
    "axislegend(ax2, position = :rt)\n",
    "fig2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "baaedd59",
   "metadata": {},
   "source": [
    "### Continuously Differentiable Functions\n",
    "\n",
    "What are the analogous results for (several times) continuously differentiable functions? We can look at a simple example that modifies the Agnesi function a little bit.\n",
    "$$\n",
    "f(x) = \\frac{1}{1 + 10 |\\sin(x/2)|^{5/2}}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f4cf9903",
   "metadata": {},
   "outputs": [],
   "source": [
    "f2 = x -> 1 / (1 + 10 * abs(sin(x/2))^(5/2))\n",
    "\n",
    "xp = range(-π, π, length=200)\n",
    "fig3 = Figure(size = (400, 250)); ax3 = Axis(fig3[1,1])\n",
    "lines!(xp, f2.(xp))\n",
    "lines!(xp, f.(xp), alpha = 0.5) # previous example\n",
    "fig3\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bb4a2ed8",
   "metadata": {},
   "outputs": [],
   "source": [
    "F̂2, kk2 = compute_fcoeffs(f2, 40);\n",
    "\n",
    "fig4 = Figure(size = (400, 250))\n",
    "ax4 = Axis(fig4[1,1], xscale = log10, yscale = log10, xlabel = L\"|k|\", ylabel = L\"|\\hat{f}_k|\", limits = (1, 45, nothing, nothing))\n",
    "scatter!(ax4, abs.(kk2), abs.(F̂2), label = L\"\\hat{f}_{2,k}\")\n",
    "scatter!(ax4, abs.(kk), abs.(F̂), label = L\"\\hat{f}_{1,k}\")\n",
    "lines!(ax4, kk2[50:end], 0.7*abs.(kk2[50:end]).^(-7/2), linestyle = :dash, color = :black)\n",
    "lines!(ax4, kk[30:end], 0.1*exp.(-α * kk[30:end]), linestyle = :dash, color = :black)\n",
    "axislegend(ax4, position = :rt)\n",
    "fig4 "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "deb8fb93",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
