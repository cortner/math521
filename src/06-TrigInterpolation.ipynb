{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aacdae15",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg; Pkg.activate(\".\")\n",
    "using CairoMakie, LinearAlgebra, LaTeXStrings, FFTA"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ab0efb83",
   "metadata": {},
   "source": [
    "## Trigonometric Interpolation\n",
    "\n",
    "This notebook goes through a series of numerical experiments testing the accuracy of trigonometric interpolation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "52fa5823",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"interpolation nodes\"\n",
    "xgrid(N) = [ j * π / N  for j = 0:2N-1 ]\n",
    "\n",
    "\"fourier coefficient indices\"\n",
    "kgrid(N) = [ 0:N; -N+1:-1 ]\n",
    "\n",
    "\"\"\"\n",
    "construct the coefficients of the trigonometric interpolant\n",
    "\"\"\"\n",
    "function triginterp_fft(f::Function, N)\n",
    "    X = xgrid(N)\n",
    "    # nodal values at interpolation nodes\n",
    "    F = f.(X) \n",
    "    return fft(F) / (2*N)\n",
    "end \n",
    "\n",
    "\n",
    "\"\"\"\n",
    "to evaluate a trigonometric polynomial just sum coefficients * basis\n",
    "we the take the real part because we assume the function we are \n",
    "approximating is real.\n",
    "\"\"\"\n",
    "evaltrig(x, F̂) = sum( real(F̂k * exp(im * x * k))\n",
    "                      for (F̂k, k) in zip(F̂, kgrid(length(F̂) ÷ 2)) )\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "08b0f512",
   "metadata": {},
   "source": [
    "We return to our periodic witch of Agnesi example."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2b26d0aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "f, α = let c = 3\n",
    "    (x -> 1 / (1 + c^2 * sin(x/2)^2)), 2*asinh(1/c)\n",
    "end\n",
    "\n",
    "fig1 = Figure(size = (400, 250)); ax1 = Axis(fig1[1,1])\n",
    "lines!(ax1, -π..π, f); fig1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "84e11ae8",
   "metadata": {},
   "outputs": [],
   "source": [
    "NN = [4, 8, 12, 16, 20, 24, 28]\n",
    "# NN = 8:8:128\n",
    "xe = range(-π, π, length=1000)\n",
    "f_ex = f.(xe)\n",
    "err_max = zeros(length(NN))\n",
    "for (i, N) in enumerate(NN)\n",
    "    F̂ = triginterp_fft(f, N);\n",
    "    fN = evaltrig.(xe, Ref(F̂))\n",
    "    err_max[i] = norm(f_ex - fN, Inf)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3cd0badf",
   "metadata": {},
   "outputs": [],
   "source": [
    "fig2 = Figure(size = (400, 300))\n",
    "ax2 = Axis(fig2[1, 1], xlabel = L\"N\", ylabel = \"\", yscale = log10)\n",
    "scatterlines!(ax2, NN, err_max, label=L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "lines!(ax2, NN[4:end], 2*exp.(-α * NN[4:end]), linestyle=:dash, color=:black, label=L\"e^{-\\alpha N}\")\n",
    "axislegend(ax2, position=:rt)\n",
    "# fig2\n",
    "fig2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "96f58415",
   "metadata": {},
   "source": [
    "Now that we have a fast algorithm, we can make the problem a bit harder."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "688b3fd4",
   "metadata": {},
   "outputs": [],
   "source": [
    "f, α = let c = 20\n",
    "    (x -> 1 / (1 + c^2 * sin(x/2)^2)), 2*asinh(1/c)\n",
    "end\n",
    "\n",
    "NN = 16:32:512\n",
    "xe = range(-π, π, length=4000)\n",
    "f_ex = f.(xe)\n",
    "err_max = zeros(length(NN))\n",
    "for (i, N) in enumerate(NN)\n",
    "    F̂ = triginterp_fft(f, N);\n",
    "    fN = evaltrig.(xe, Ref(F̂))\n",
    "    err_max[i] = norm(f_ex - fN, Inf)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bf928247",
   "metadata": {},
   "outputs": [],
   "source": [
    "fig3 = Figure(size = (400, 300))\n",
    "ax3 = Axis(fig3[1, 1], xlabel = L\"N\", ylabel = \"\", yscale = log10)\n",
    "scatterlines!(ax3, NN, err_max, label=L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "lines!(ax3, NN[4:10], 10*exp.(-α * NN[4:10]), linestyle=:dash, color=:black, label=L\"\\sim e^{-\\alpha N}\")\n",
    "axislegend(ax3, position=:rt)\n",
    "fig3"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7f044666",
   "metadata": {},
   "source": [
    "To estimate the errors we are evaluating the trigonometric interpolant on an increasing number of grid points. That still has quadratic scaling! But this limitation can also be removed."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f89ff0fc",
   "metadata": {},
   "source": [
    "### Evaluating Trigonometric Polynomials on a Grid"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "190d063b",
   "metadata": {},
   "source": [
    "The key observation is that the IFFT can be used to obtain the nodal values. So to get nodal values on a refined grid we can simply \"pad\" the vector $\\hat{F}$ with zeros. This is what `evaltrig_grid` does."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "523dcf1e",
   "metadata": {},
   "outputs": [],
   "source": [
    "function evaltrig_grid(F̂, M::Integer; convert = real)\n",
    "    N = length(F̂) ÷ 2\n",
    "    @assert 2 * N == length(F̂)\n",
    "    @assert M >= N \n",
    "    F̂_M = zeros(ComplexF64, 2*M)\n",
    "    F̂_M[1:N] .= F̂[1:N]\n",
    "    F̂_M[end-N+1:end] .= F̂[end-N+1:end]\n",
    "    x = xgrid(M) \n",
    "    Fx = real.(ifft(F̂_M) * (2*M))\n",
    "    return Fx, x\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cf3a9281",
   "metadata": {},
   "outputs": [],
   "source": [
    "N = 3\n",
    "F̂ = randn(2*N)\n",
    "\n",
    "# naive plotting just the 6 nodal values?\n",
    "xp1 = [xgrid(N); [2*π]]\n",
    "F1 = real.(ifft(F̂)) * (2*N); push!(F1, F1[1])\n",
    "\n",
    "# We can of course just use eval_trig on a finer grid\n",
    "xp2 = range(0, 2*π, length=200)\n",
    "F2 = evaltrig.(xp2, Ref(F̂))\n",
    "\n",
    "# Nlog(N) scaling cost \n",
    "F3, xp3 = evaltrig_grid(F̂, 200)\n",
    "push!(F3, F3[1]); xp3 = [xp3; [2*π]]\n",
    "\n",
    "fig4 = Figure(size = (400, 300)); ax4 = Axis(fig4[1,1])\n",
    "lines!(ax4, xp1, F1, label = \"pw\")\n",
    "lines!(ax4, xp2, F2, label = \"evaltrig\")\n",
    "lines!(ax4, xp3, F3, label = \"evaltrig_grid\", linestyle = :dash)\n",
    "fig4"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f47662b1",
   "metadata": {},
   "source": [
    "Equipped with this technique we can now easily scale the error analysis to arbitarily large problems. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "82638688",
   "metadata": {},
   "outputs": [],
   "source": [
    "f, α = let c = 100\n",
    "    (x -> 1 / (1 + c^2 * sin(x/2)^2)), 2*asinh(1/c)\n",
    "end\n",
    "\n",
    "fig5 = Figure(size = (400, 250)); ax5 = Axis(fig5[1,1])\n",
    "lines!(ax5, -π..π, f); fig5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1ec58ae4",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "# NN = 16:32:512\n",
    "NN = 128:128:(16*128)   # 16 * 128 = 2048\n",
    "err_max = zeros(length(NN))\n",
    "Nerr = 2^15  # ≈ 32000\n",
    "for (i, N) in enumerate(NN)\n",
    "    F̂ = triginterp_fft(f, N);\n",
    "    Fe, xe = evaltrig_grid(F̂, Nerr)\n",
    "    err_max[i] = norm(real.(Fe) - f.(xe), Inf)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3cabb4d4",
   "metadata": {},
   "outputs": [],
   "source": [
    "fig6 = Figure(size = (400, 300))\n",
    "ax6 = Axis(fig6[1, 1], xlabel = L\"N\", ylabel = \"\", yscale = log10)\n",
    "scatterlines!(ax6, NN, err_max, label=L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "lines!(ax6, NN[4:10], 10*exp.(-α * NN[4:10]), linestyle=:dash, color=:black, label=L\"\\sim e^{-\\alpha N}\")\n",
    "axislegend(ax6, position=:rt)\n",
    "fig6"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "377448e4",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "### Further Experiments\n",
    "\n",
    "So far we focused on a very limited range of target functions. Here, we can explore a few more function classes."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5a3ae5d0",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "A little utility function to help streamline numerical tests \n",
    "- take a function `f` \n",
    "- construct the trigonometric interpolant\n",
    "- evaluate the error at `Nerr` points\n",
    "- return the error in the max norm.\n",
    "\"\"\"\n",
    "function interpolation_error(f, N::Integer, Nerr = N * 8)\n",
    "    F̂ = triginterp_fft(f, N);\n",
    "    Fe, xe = evaltrig_grid(F̂, Nerr)\n",
    "    return norm(real.(Fe) - f.(xe), Inf)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f3a60b25",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Example: Periodic Fermi-Dirac Function\n",
    "\n",
    "# define a new function\n",
    "β = 100   # Fermi - temperature \n",
    "f1 = x -> 1 / (1 + exp(β*sin(x)))\n",
    "α1 = π/β    # NB: only asymptotically exact\n",
    "\n",
    "# compute errors\n",
    "NN = 16:16:(10*16); Nerr = 2^12\n",
    "# NN = 64:64:(10*64); Nerr = 2^14\n",
    "max_err = interpolation_error.(Ref(f1), NN, Nerr)\n",
    "\n",
    "# ---------------------------------------------\n",
    "# plot the target function\n",
    "fig_1 = Figure(size = (600, 250)) \n",
    "\n",
    "ax_11 = Axis(fig_1[1,1], xlabel = \"x\")\n",
    "lines!(-π..π, f1, label = L\"f\")\n",
    "axislegend(ax_11)\n",
    "\n",
    "ax_12 = Axis(fig_1[1,2], yscale = log10, xlabel = \"N\")\n",
    "# ylims!(ax_32, (1e-11, 1e-3))\n",
    "scatterlines!(ax_12, NN, max_err, label = L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "lines!(ax_12, NN[6:end], 2*exp.(-α1 * NN[6:end]), linestyle = :dash, color = :black, label = L\"e^{-\\alpha N}\")\n",
    "axislegend(ax_12, position=:rt)\n",
    "fig_1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d3381102",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Example: A Singular function C^{2,1} - regularity\n",
    "#     not analytic so we only get algebraic decay.\n",
    "\n",
    "# define a new function\n",
    "f2 = x -> abs(sin(x/2))^3\n",
    "α2 = 3.0\n",
    "\n",
    "# compute errors\n",
    "NN = (2).^(3:2:16); Nerr = 8 * NN[end]\n",
    "max_err = interpolation_error.(Ref(f2), NN, Nerr)\n",
    "\n",
    "\n",
    "# # plot the target function\n",
    "# p1 = plot(f, -π/2, π/2, lw=3, label = \"f\", xlabel = \"x\")\n",
    "\n",
    "\n",
    "# ---------------------------------------------\n",
    "# plot the target function\n",
    "fig_2 = Figure(size = (600, 250)) \n",
    "\n",
    "ax_21 = Axis(fig_2[1,1], xlabel = \"x\")\n",
    "lines!(-π..π, f2, label = L\"f\")\n",
    "axislegend(ax_21, position=:ct)\n",
    "\n",
    "ax_22 = Axis(fig_2[1,2], yscale = log10, xscale = log10, xlabel = \"N\")\n",
    "ylims!(ax_22, (1e-12, 1e-3))\n",
    "scatterlines!(ax_22, NN, max_err, label = L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "lines!(ax_22, NN[2:5], (NN[2:5]).^(-α2), linestyle = :dash, color = :black, label = L\"N^{-\\alpha}\")\n",
    "axislegend(ax_22, position=:rt)\n",
    "fig_2\n",
    "\n",
    "\n",
    "# # plot errors \n",
    "# p2 = plot(NN, max_err; lw=3, m=:o, ms=5, label = L\"\\|f - I_N f\\|_\\infty\",\n",
    "#     yscale = :log10, xscale = :log10, xlabel = L\"N\", \n",
    "#     size = (400, 300), ylims = (1e-16, 1.0))\n",
    "# plot!(p2, NN[4:end], 10*(NN[4:end]).^(-α); lw=2, ls=:dash, c = :black, \n",
    "#         label = L\"N^{-\\alpha}\")\n",
    "\n",
    "# # plot both \n",
    "# plot(p1, p2, layout = (1,2), size = (600, 300))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d11f90f8",
   "metadata": {},
   "source": [
    "---"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "000fd06b",
   "metadata": {},
   "outputs": [],
   "source": [
    "# define a new function\n",
    "f3 = x -> abs(sin(x/2))^(2+1/3)\n",
    "α3 = 2+1/3\n",
    "\n",
    "# compute errors\n",
    "NN = (2).^(3:2:13); Nerr = 8 * NN[end]\n",
    "max_err = interpolation_error.(Ref(f3), NN, Nerr)\n",
    "\n",
    "# ---------------------------------------------\n",
    "# plot the target function\n",
    "fig_3 = Figure(size = (600, 250)) \n",
    "\n",
    "ax_31 = Axis(fig_3[1,1], xlabel = \"x\")\n",
    "lines!(-π..π, f3, label = L\"f\")\n",
    "axislegend(ax_31, position=:ct)\n",
    "\n",
    "ax_32 = Axis(fig_3[1,2], yscale = log10, xscale = log10, xlabel = \"N\")\n",
    "ylims!(ax_32, (1e-11, 1e-3))\n",
    "scatterlines!(ax_32, NN, max_err, label = L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "lines!(ax_32, NN[3:end], (NN[3:end]).^(-α3), linestyle = :dash, color = :black, label = L\"N^{-\\alpha}\")\n",
    "axislegend(ax_32, position=:rt)\n",
    "fig_3"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "804b864f",
   "metadata": {},
   "source": [
    "---"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bca881ec",
   "metadata": {},
   "outputs": [],
   "source": [
    "# define a new function\n",
    "function finf(x)\n",
    "    if 0 < x < π \n",
    "        return exp(-1/sin(x)) \n",
    "    else\n",
    "        return zero(typeof(x))\n",
    "    end\n",
    "end\n",
    "\n",
    "# compute errors, twice: with linear and exponential scaling\n",
    "NN1 = 8:32:512; Nerr = 8 * NN1[end]\n",
    "max_err1 = interpolation_error.(Ref(finf), NN1, Nerr)\n",
    "NN2 = (2).^(2:10); Nerr = 8 * NN2[end]\n",
    "max_err2 = interpolation_error.(Ref(finf), NN2, Nerr)\n",
    "\n",
    "\n",
    "# ---------------------------------------------\n",
    "# plot the target function here \n",
    "#   + errors in both linear and log scales!!\n",
    "\n",
    "fig_4 = Figure(size = (600, 200)) \n",
    "\n",
    "ax_41 = Axis(fig_4[1,1], xlabel = \"x\")\n",
    "lines!(-π..π, finf, label = L\"f\")\n",
    "axislegend(ax_41, position=:lt)\n",
    "\n",
    "ax_42 = Axis(fig_4[1,2], yscale = log10, xscale = identity, xlabel = \"N\")\n",
    "ylims!(ax_42, (1e-14, 1e-1))\n",
    "scatterlines!(ax_42, NN1, max_err1, label = L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "\n",
    "ax_43 = Axis(fig_4[1,3], yscale = log10, xscale = log10, xlabel = \"N\")\n",
    "ylims!(ax_42, (1e-14, 1e-1))\n",
    "scatterlines!(ax_43, NN2, max_err2, label = L\"\\Vert f - I_N f \\Vert_\\infty\")\n",
    "\n",
    "fig_4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0da4810e",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
