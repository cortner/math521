{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aacdae15",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg; Pkg.activate(\".\")\n",
    "using CairoMakie, LinearAlgebra, LaTeXStrings, ForwardDiff, FFTA\n",
    "include(\"spectral_tools.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "08b0f512",
   "metadata": {},
   "source": [
    "# Fourier Spectral Methods (1D)\n",
    "\n",
    "## Revise Motivating Example \n",
    "\n",
    "We return to the boundary value problem with periodic boundary conditions: \n",
    "$$\n",
    "\\begin{aligned}\n",
    "  - u'' + u &= f, \\quad x \\in (-\\pi, \\pi), \\\\ \n",
    "  u(-\\pi) &= u(\\pi), \\\\ \n",
    "  u'(-\\pi) &= u'(\\pi)\n",
    "\\end{aligned}\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2b26d0aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "# (1) let's take our periodic agnesi example as f\n",
    "f_fun, α = let c = 3\n",
    "    (x -> 1 / (1 + c^2 * sin(x/2)^2)), 2*asinh(1/c)\n",
    "end\n",
    "\n",
    "# (2) before we constructed the projection, now we use the \n",
    "#     trigonometric interpolant \n",
    "N = 10\n",
    "F̂ = triginterp_fft(f_fun, N)\n",
    "\n",
    "# (3) solve the PDE in reciprocal space\n",
    "#     note Û represents the solution. We are done here!\n",
    "K = kgrid(N)\n",
    "Û = F̂ ./ (1 .+ abs.(K).^2)\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "35d2287f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# (4) To plot the solution we just evaluate it on a \n",
    "# refined grid \n",
    "\n",
    "Up, Xp = evaltrig_grid(Û, N)\n",
    "up, xp = evaltrig_grid(Û, 128)\n",
    "\n",
    "fig1 = Figure(size=(400, 250)); ax1 = Axis(fig1[1,1])\n",
    "lines!(ax1, xp, up, label = L\"u_N(x)\")\n",
    "scatter!(ax1, Xp, Up, label = \"nodal vals\")\n",
    "axislegend(ax1, position = :ct)\n",
    "fig1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0da4810e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# define a known exact solution and compute the resulting f\n",
    "u_ex, α = let c = 3\n",
    "    (x -> 1 / (1 + c^2 * sin(x)^2)), asinh(1/c)\n",
    "end\n",
    "du_ex = x -> ForwardDiff.derivative(u_ex, x)\n",
    "f_fun = x -> u_ex(x) - ForwardDiff.derivative(du_ex, x)\n",
    "\n",
    "function solve_and_err(N; Ne = 1024)\n",
    "    K = kgrid(N)\n",
    "    F̂ = triginterp_fft(f_fun, N)\n",
    "    Û = F̂ ./ (1 .+ K.^2)\n",
    "    Xe = xgrid(Ne); Ue = u_ex.(Xe); ∇Ue = du_ex.(Xe)\n",
    "    U, _ = evaltrig_grid(Û, Ne)\n",
    "    ∇U, _ = evaltrig_grid(im * K .* Û, Ne)\n",
    "    return norm(U - Ue, Inf), norm(∇U - ∇Ue, Inf)\n",
    "end\n",
    "\n",
    "# looking at errors for the spectral method\n",
    "NN = 8:8:128\n",
    "errs = solve_and_err.(NN)\n",
    "errs0 = [e[1] for e in errs]\n",
    "errs1 = [e[2] for e in errs]\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "093f8d84",
   "metadata": {},
   "outputs": [],
   "source": [
    "fig2 = Figure(size=(400, 300))\n",
    "ax2 = Axis(fig2[1,1], xscale = identity, yscale = log10, xlabel = L\"N\", ylabel = \"error\")\n",
    "scatterlines!(ax2, NN, errs0, label = L\"\\Vert u_N - u\\Vert_\\infty\")\n",
    "scatterlines!(ax2, NN, errs1, label = L\"\\Vert u_N' - u'\\Vert_\\infty\")\n",
    "lines!(ax2, NN[6:end-4], 10*exp.(- α * NN[6:end-4]), linestyle=:dash, color = :black, label = L\"\\sim e^{-\\alpha N}\")\n",
    "axislegend(ax2, position=:rt)\n",
    "fig2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "621a3357",
   "metadata": {},
   "source": [
    "---"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "706c1523",
   "metadata": {},
   "source": [
    "### Preview: A Transport Problem\n",
    "\n",
    "$$\n",
    "\tu_t + c u_x = 0 \\qquad \\text{+ PBC}\n",
    "$$\n",
    "For now we take $c$ to be constant. Below we will switch to $x$-dependent $c$.\n",
    "\n",
    "First discretise in time using the Leapfrog scheme \n",
    "$$\n",
    "\t\\frac{u^{n+1} - u^{n-1}}{2 \\Delta t} + c (u^n)_x = 0.\n",
    "$$\n",
    "\n",
    "We will also compare this to a finite-difference scheme."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a9de5015",
   "metadata": {},
   "outputs": [],
   "source": [
    "# ------------------------------------------\n",
    "# Problem setup\n",
    "N = 64  \n",
    "dt = π/(6N) \n",
    "tmax = 32.0\n",
    "c = 1.0\n",
    "u0 = x ->  exp(- 10 * (1+cos(x))/2 )\n",
    "#------------------------------------------\n",
    "\n",
    "X = xgrid(N)\n",
    "K = kgrid(N)\n",
    "\n",
    "# differentiation operator in Fourier space \n",
    "D̂ = im*K\n",
    "\n",
    "# initial condition, we also need one additional v in the past\n",
    "# (this takes one step of the PDE backward in time)\n",
    "V = u0.(X)\n",
    "Vold = V + dt * c * real.( ifft( D̂ .* fft(V) ) ) \n",
    "\n",
    "# Setup for the FD Scheme, UPWIND D1 and CENTERED D2\n",
    "h = π/N\n",
    "D1 = collect(Bidiagonal(ones(2*N)/h, -ones(2*N-1)/h, :L))\n",
    "D1[1, 2*N] = -1/h # (PBC)\n",
    "D2 = collect(Tridiagonal(-ones(2*N-1)/(2h), zeros(2*N), ones(2*N-1)/(2h)))\n",
    "D2[1,2*N] = -1/(2h); D2[2*N,1] = 1/(2h)\n",
    "V1h = V; V2h = V; V2hold = Vold\n",
    "\n",
    "# ------------- setup the plotting for an animation\n",
    "\n",
    "t_obs = Observable(0.0)\n",
    "V_obs = Observable(V)\n",
    "V1h_obs = Observable(V1h)\n",
    "V2h_obs = Observable(V2h)\n",
    "\n",
    "fig = Figure(size = (500, 250))\n",
    "ax = Axis(fig[1,1], title = \"t = $(round(t_obs, digits=2))\")\n",
    "limits!(ax, 0.0, 2*π, -0.1, 1.2)\n",
    "lines!(ax, X, (@lift u0.(X .- $t_obs)), label=\"exact\")\n",
    "lines!(ax, X, V_obs, label=\"spectral\")\n",
    "lines!(ax, X, V1h_obs, label=\"upwind\", linestyle=:dash)\n",
    "lines!(ax, X, V2h_obs, label=\"centered\", linestyle=:dash)\n",
    "Legend(fig[1,2], ax)\n",
    "\n",
    "record(fig, \"animation.gif\", 0:dt:tmax; framerate=60) do t\n",
    "    global V, Vold, V1h, V2h, V2hold, D1, D2, D̂, obs\n",
    "    # SPECTRAL \n",
    "    # differentiation in reciprocal space\n",
    "    W = real.( ifft( D̂ .* fft(V) ) )   \n",
    "    # multiplication and update in real space\n",
    "    V, Vold = Vold - 2 * dt * c * W, V\n",
    "\n",
    "    # FINITE-DIFFERENCE\n",
    "    V1h = V1h - dt * c * (D1 * V1h)  # (upwind)\n",
    "    V2h, V2hold = V2hold - 2*dt * c * (D2*V2h), V2h     #(centered)  \n",
    "    \n",
    "    # update the plot \n",
    "    t_obs[] = t \n",
    "    V_obs[] = V \n",
    "    V1h_obs[] = V1h\n",
    "    V2h_obs[] = V2h\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "470a02ef",
   "metadata": {},
   "outputs": [],
   "source": [
    "display(\"text/html\", \"<img src='animation.gif' width=400px>\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5f0772e8",
   "metadata": {},
   "outputs": [],
   "source": [
    "# plot the final solution again to see the numerical damping and dispersion\n",
    "\n",
    "fig = Figure(size = (500, 250))\n",
    "ax = Axis(fig[1,1], title = \"t = $tmax\")\n",
    "limits!(ax, 0.0, 2*π, -0.1, 1.2)\n",
    "lines!(ax, X, u0.(X .- tmax), label=\"exact\")\n",
    "lines!(ax, X, V, label=\"spectral\")\n",
    "lines!(ax, X, V1h, label=\"upwind\", linestyle=:dash)\n",
    "lines!(ax, X, V2h, label=\"centered\", linestyle=:dash)\n",
    "Legend(fig[1,2], ax)\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9bcde189",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
